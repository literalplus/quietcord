import org.apache.tools.ant.filters.ReplaceTokens

// -- project metadata
group 'io.github.xxyy'
version '1.0'

// -- build plugins
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'net.linguica.maven-settings'

// -- project properties
defaultTasks 'clean', 'build', 'publishToMavenLocal'
sourceCompatibility = 1.7
def commitHash = 'git rev-parse --short HEAD'.execute().text.trim()
def fullVersion = version + '-git-' + commitHash
def buildName = commitHash + '-@-' + new Date().format('yyyy-MM-dd HH:mm')

// -- build dependencies
repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile group: 'net.md-5', name: 'bungeecord-api', version: '1.8-SNAPSHOT'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// -- deployment
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }

    repositories {
        maven { //reads from Maven settings.xml (~/.m2/settings.xml) repo definition with that id
            name quietcord_deploy_repo_id
            url quietcord_deploy_repo_url
        }
    }
}

// -- buildscript dependencies
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.linguica.gradle:maven-settings-plugin:0.4'
        classpath 'org.ajoberstar:grgit:1.4.0'
    }
}

// -- plugin configurations
jar {
    manifest.attributes "Implementation-Title": project.name
    manifest.attributes "Implementation-Version": project.version
    manifest.attributes "Implementation-Build": buildName
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}


configure(tasks.processResources) {
    include '**/*.yml'
    filter(ReplaceTokens,
            tokens: [version: fullVersion])
}

